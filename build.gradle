/*
 * Copyright 2017 Boleslav Bobcik - Auderis
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		classpath 'cz.auderis:auderis-gradle-tools:1.0.1'
	}
}

import cz.auderis.tools.gradle.SemanticVersion

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group 'cz.auderis'
version SemanticVersion.from('VERSION')
ext.releaseMode = (version.stable && ((!hasProperty('localRelease') || !getProperty('localRelease'))))

compileJava {
	sourceCompatibility = '1.7'
	targetCompatibility = '1.7'
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile 'org.apache.commons:commons-math3:3.6.1'
	compile 'com.google.guava:guava:23.0'

	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile('pl.pragmatists:JUnitParams:1.0.6') {
		exclude group: 'junit'
	}
	testCompile('junit:junit:4.12') {
		exclude group: 'org.hamcrest'
	}
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'cz.auderis:auderis-test-category:1.1.0'
	testCompile 'cz.auderis:auderis-test-extra:1.3.7'
}

tasks.javadoc.classpath = sourceSets.main.compileClasspath

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}
